//-----------------------------------------------------------------------------
// Copyright (c) 2024 Electronic Arts.  All rights reserved.
//-----------------------------------------------------------------------------

//!include particle.inc
//!include simConstants.inc

// Code-generated by UniformBufferFactory
//!insert RenderConstants

// const values like RenderModeStandard etc
// that are plugged in from js 
//!insert RenderEnums

@group(0) @binding(0) var<uniform> g_constants : RenderConstants;
@group(0) @binding(1) var<storage> g_particles : array<Particle>;


struct VertexOutput {
    @builtin(position) interpolatedVertexPosition : vec4f,
    @location(0) particlePosition : vec2f,
    @location(1) vertexPosition : vec2f,
    @location(2) particleColor : vec3f,
    @location(3) particleRadius : f32,
};

const s_quadVertices = array(
    vec2f(-1, -1),
    vec2f(1, -1),
    vec2f(1, 1),
    vec2f(-1, -1),
    vec2f(1, 1),
    vec2f(-1, 1)
);

const s_antialiasingWidth = 2000.0;

@vertex
fn vertexMain(@builtin(vertex_index) vertexId: u32, @builtin(instance_index) instanceId : u32) -> VertexOutput {
    let onePixel = 1.0 / f32(g_constants.canvasSize.x);

    let particle = g_particles[instanceId];

    if(particle.enabled == 0)
    {
        return VertexOutput(
            vec4f(0,0,0,0),
            vec2f(0,0),
            vec2f(0,0),
            vec3f(0,0,0),
            0.0
        );
    }

    var particlePosition : vec2f = particle.position;

    var particleMaterialRadius = g_constants.particleRadiusTimestamp.x * sqrt(particle.volume);

    var quadVertexPosition = s_quadVertices[vertexId] * (1+s_antialiasingWidth*onePixel)*particleMaterialRadius;
    var vertexPosition = quadVertexPosition + particlePosition;

    var vertexPositionInRenderSpace = (vertexPosition - g_constants.viewPos) / g_constants.viewExtent;

    var color = particle.color;

    if(g_constants.renderMode == RenderModeStandard)
    {

    }
    if(g_constants.renderMode == RenderModeCompression)
    {
        let density = particle.liquidDensity;
        color = vec3f(-5*log(density), 5*log(density), color.z);
    }
    else if(g_constants.renderMode == RenderModeVelocity)
    {
        color = vec3f((particle.displacement*g_constants.deltaTime*1000.0 + 0.5f), color.z);
    }

    return VertexOutput(
        vec4f(vertexPositionInRenderSpace, 0, 1),
        particlePosition,
        vertexPosition,
        color,
        particleMaterialRadius,
    );
}

@fragment
fn fragmentMain(input: VertexOutput) -> @location(0) vec4f {
    let onePixel = 1.0 / f32(g_constants.canvasSize.x);

    let particleRadius = input.particleRadius;
    let fragmentPosition = input.vertexPosition;
    let fragOffsetFromCenter = fragmentPosition - input.particlePosition;
    let distanceFromCenter = length(fragOffsetFromCenter);

    let alpha = smoothstep(1.0, 0.0, (distanceFromCenter-particleRadius)/(onePixel*s_antialiasingWidth*particleRadius) );

    return vec4f(input.particleColor,alpha);
}